ZVSE2
; Author:   Archer30
; Engine:   ERM 2.0+
; Requires: ERA 3.3+, Era Erm Framework

; Get the clicked artifact slot ID, works for either hero screen or hero meeting screen.


!?FU(art_GetClickedArtSlot);
!#VA(item:x);                           [CM:I value]
!#VA(artSlot:x);                        [Out. Clicked artifact slot]
!#VA(hero:x);                           [Out. CM:H. Clicked hero]

!!VR(artSlot):S-1;
!!VR(hero):S(NO_HERO);

!!SN:F^GetGameState^/?v2;
; Exit if not in Hero Screen
!!FU&v3<>(DLG_HERO_SCREEN)/v3<>(DLG_HERO_MEETING_SCREEN):E;

!!CM:H?(leftHero:y)/?(rightHero:y);

; Hero Screen
!!if&v3=(DLG_HERO_SCREEN);
  ; Eeuipment slots
  !!if&(item)<=20;
    !!VR(artSlot):S(item) -2;
  ; Backpack slots
  !!el&(item)>=40/(item)<=44;
    !!VR(artSlot):S(item) -21;
  !!en;

  !!VR(hero):S(leftHero);
; Hero Meeting Screen
!!el;
  ; Left hero equipments
  !!if&(item)<=45;
    !!VR(artSlot):S(item) -27;
    !!VR(hero):S(leftHero);
  ; Right hero equipments
  !!el&(item)>45/(item)<=64;
    !!VR(artSlot):S(item) -46;
    !!VR(hero):S(rightHero);
  ; Left hero backpack
  !!el&(item)>=89/(item)<=93;
    !!VR(artSlot):S(item) -70;
    !!VR(hero):S(leftHero);
  ; Right hero backpack
  !!el&(item)>93/(item)<=98;
    !!VR(artSlot):S(item) -75;
    !!VR(hero):S(rightHero);
  !!en;
!!en;
