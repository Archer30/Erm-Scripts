ZVSE2
; Author:   Archer30
; Engine:   ERM 2.0+
; Requires: ERA 3.3+, Era Erm Framework

; Resource Silos now provide 1 wood/1 ore and 1 special resource accoding to types of towns.


!?FU(OnEveryDay)&i^timerOnce^/i^timerDay^>1;
!!VR(x:y):S-1;

; Loop through all towns
!!re i;
  !!UN:U(OBJ_TOWN)/(ANY_OBJ)/-1/(x)/(y:y)/(z:y);
  !!br&(x)<0;

  ; Next town if silos is not built/town belongs to no player
  !!CA(x)/(y)/(z):O?(owner:y) B3/15;
  !!co|-1/(owner)=(NO_OWNER);

  ; Check for type of town and add in additional res
  !!CA(x)/(y)/(z):T?(townType:y);

  !!if&(townType)=(TOWN_CASTLE);
    !!OW:R(owner)/(RES_GEMS)/d1;
  !!el|(townType)=(TOWN_RAMPART)/(townType)=(TOWN_TOWER)/(townType)=(TOWN_INFERNO)/(townType)=(TOWN_DUNGEON)/(townType)=(TOWN_CONFLUX);
    !!OW:R(owner)/(RES_WOOD)/d1;
    !!OW:R(owner)/(RES_ORE)/d1;
  !!el&(townType)=(TOWN_NECROPOLIS);
    !!OW:R(owner)/(RES_MERCURY)/d1;
  !!el&(townType)=(TOWN_STRONGHOLD);
    !!OW:R(owner)/(RES_CRYSTAL)/d1;
  !!el&(townType)=(TOWN_FORTRESS);
    !!OW:R(owner)/(RES_SULFUR)/d1;
  !!en;
!!en;

!!UN:R2;                                [refresh resouces bar]
