ZVSE2
; Author:   daemon_n
; Engine:   ERM 2.0+
; Requires: ERA 3.3+, Era Erm Framework

; Disable Invite Hero feature frmm HoMM 3 HD regarless the settings in era.ini


!?FU(OnStartOrLoad);
!!SN:F^GetModuleHandleA^/^HD_WOG.dll^;
!!FU&v1=(FALSE):E;

!!SN:L^erm_hooker.era^/?(hooker:y);
!!FU&(hooker)=0:E;

!!SN:A(hooker)/^SetHook^/?(hookingFuncAddress:y);
!!FU(invite_CreateERMHook):P(hookingFuncAddress);

!?FU(invite_CreateERMHook);
!#VA(address:x);

!!SN:E(address)/1/6128160/(invite_OnOpenTavernDlg); [On opening tavern dialogue]

!?FU(invite_OnOpenTavernDlg);
!#VA(address:x);

!!UN:C(address)/(STRUCT_HOOK_CONTEXT_ECX)/(UNC_INT)/?(dlg:y);

!!re i/400/402;
  !!FU(invite_DlgShowItem):Pi/0/0/0/(dlg);
!!en;

!!FU(invite_DlgShowItem):P0/0/0/0/(dlg);[remove "invite hero" text]

!?FU(invite_DlgShowItem);
!#VA(itemId:x) (showItem:x) (clickableItem:x) (redraw:x) (customDlgStruct:x);

!!if&(itemId)<0;
  !!IF:M^itemId is incorrect!
  should be 0 and higher^;
  !!FU:E;
!!en;

!!VR(savedV1:y):Sv1;

!!if&(customDlgStruct);
  !!VR(currentDlgStruct:y):S(customDlgStruct);
!!el;
  !!FU(H3Dlg_GetCurrentDlg):P?(currentDlgStruct:y);
!!en;

!!SN:E6288816/2/(currentDlgStruct)/(itemId);

!!if&v1<1;

  !!FU:E;
!!en;
!!VR(itemStructure:y):Sv1; 

!!if&(showItem);

  !!SN:E6286720/2/(itemStructure)/5/6;
!!el;

  !!if&(clickableItem);
    !!SN:E6286720/2/(itemStructure)/6/4;
  !!el;
    !!SN:E6286720/2/(itemStructure)/6/6;
  !!en;

!!en;

!!SN&(redraw):E6288864/2/(currentDlgStruct)/1/-65535/65535; [update currentDlg;]

!!VRv1:S(savedV1);
