ZVSE2
; Author:   Archer30 and Perry R
; Special thanks to Hawaiing
; Engine:   ERM 2.0+
; Requires: ERA 3.3+, Era Erm Framework

; Allows you to tweak the stats of  Santa Gremlins and the Santa Guards. Also allows you to enable gift ability.


; ============== SETTINGS ==============
!#VRi^santas_on^:S(TRUE);               [This script will be activated only if true]
!#SN:W^Santas_guards_health^/2;
!#SN:W^Santas_guards_attack^/2;
!#SN:W^Santas_guards_defense^/2;
!#SN:W^Santas_guards_shots^/1;        
!#SN:W^Santas_guards_casts^/1;
!#SN:W^Santas_damage_scaling^/5;        [Ice Bolt damage per unit]
!#VRi^santas_enableGift^:S(FALSE);
; ============ END SETTINGS ============


!?FU(santa_RestrainValues);
!!VRi^Santas_guards_health^:F1/(INT_MAX);
!!VRi^Santas_guards_attack^:F0/(INT_MAX);
!!VRi^Santas_guards_defense^:F0/(INT_MAX);
!!VRi^Santas_guards_shots^:F0/(INT_MAX);
!!VRi^Santas_guards_casts^:F0/(INT_MAX);
!!VRi^Santas_damage_scaling^:F0/(INT_MAX);

!#FU(santa_RestrainValues);

!?FU(OnBattleRound)&i^battle_round^=0;
!!UN:C42336844/4/?(value:y);               [Hawaiing]
!!VR(value):-1;

!!re i/0/(value)/1;
  !!VR(stackSantaAdd:y):Si *16 +42013744;
  !!VR(stackGuardAdd:y):S(stackSantaAdd)+4;
  *!VR(monGuardAdd:y):S(stackSantaAdd)+8;  [mon type and original speed of guards, currently not needed]
  *!VR(speedGuardAdd:y):S(stackSantaAdd)+12;
  !!UN:C(stackGuardAdd)/4/?(stackGuard:y); [now we have stack ids of guards]
  *!UN:C(stackSantaAdd)/4/?(stackSanta:y) C(monGuardAdd)/4/?(monGuard:y) C(speedGuardAdd)/4/?(speedGuard:y); [currently not needed]

  !!if&(stackGuard)>=0/(stackGuard)<42;
    !!BM(stackGuard):Hi^Santas_guards_health^ Ai^Santas_guards_attack^ Di^Santas_guards_defense^;
    !!BM(stackGuard):F?(flags:y) E?(casts:y);
    !!VR(flags):&4;
    !!BM(stackGuard)&(flags)=4:U3/i^Santas_guards_shots^;
    !!BM(stackGuard)&(casts)>0:Ei^Santas_guards_casts^;
  !!en;
!!en;

** Set the damage of the Ice Bolt attack from Santas
!?FU(OnMagicCorrectedResistance);

!!BG:A?(action:y) S?(spell:y) N?(stack:y); [Get Action, Spell Number and Creature Number]
!!BM(stack):T?(mon:y) N?(num:y);           [Get Creature Type and Number of Creatures]
!!MR:F?(dmg:y);

!!if&(action)=10/(spell)=16/(mon)=173/(dmg)>0/(num)>0; [If Santa Gremlins casting Ice Bolt]
  !!VR(dmgNew:y):S(num) *i^Santas_damage_scaling^; [calc new damage]
  !!MR:F(dmgNew);                       [Set new Damage]
!!en;

** Enable gift ability
!?FU(OnStartOrLoad);
!!if&i^santas_enableGift^;
  !!UN:C7711076/2/2677;
!!el;
  !!UN:C7711076/2/-28528;
!!en;
