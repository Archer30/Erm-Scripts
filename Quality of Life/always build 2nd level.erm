ZVSE2
; Author:   Archer30
; Engine:   ERM 2.0+
; Requires: ERA 3.3+, Era Erm Framework

; Loop through all towns at day 1 and build 2nd level dwellings for towns with forts, owner and 1st level dwellings.


; ============== SETTINGS ==============
!#VRi^always2lv_on^:S(TRUE);            [This script will be activated only if true]
; ============ END SETTINGS ============


!?FU(always2lv_RestrainValues);
!!VRi^always2lv_on^:F(FALSE)/(TRUE);

!#FU(always2lv_RestrainValues):P;

!?FU(OnEveryDay)&i^timerOnce^/i^timerDay^=1/i^always2lv_on^;
!!FU(always2lv_BuildDwellings):P;

!?FU(always2lv_BuildDwellings);
!!VR(x:y):S-1;

; loop through all towns
!!re i;
  !!UN:U(OBJ_TOWN)/(ANY_OBJ)/-1/(x)/(y:y)/(z:y);
  !!br&(x)<0;

  ; next town if fort is not built/belongs to no player
  !!CA(x)/(y)/(z):O?(player:y) B3/7;
  !!co|-1/(player)=(NO_PLAYER);

  ; build the 2nd level dwelling if 1st is built
  !!CA(x)/(y)/(z):B3/30;
  !!CA(x)/(y)/(z)&1:B6/31;
!!en;
