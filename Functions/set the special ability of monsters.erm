ZVSE2
; Author:   Archer30
; Special thanks to Hawaiing and Majaczek
; Engine:   ERM 2.0+
; Requires: ERA 3.3+, Era Erm Framework

; Set the special abilities of monsters, comaptible with Amethyst2_4 (Third Upgrade Mod)


!?FU(abil_SetUpMonsterSpecialAbility1);
!#VA(mon:x) (ability:x);
; 0 Drain Life - Vampire Lord
; 1 20% Thunder Clap - Thunder Bird
; 2 Death Stare - Mighty Gorgon
; 3 Dispel - Dragonfly
; 4 20% Acid Breath (damage) - Rust Dragon
; 5 No Ability
!!SN:L^amethyst2_4.dll^/?(amethyst:y);

!!if&(amethyst)<>0;
  !!SN:A(amethyst)/^ChangeCreatureTable^/?(address:y) E(address)/1/(mon)/^Attack effect=%(ability)^;
!!el;
  !!if&(mon)<(MON_VAMPIRE_LORD);
    !!IF:M^Sorry, but you can't set special ability 1 of monsters prior than #63 by this way.^;
  !!el&(ability)<0/(ability)>5;
    !!IF:M^Sorry, but the special ability 1 you set is beyond the limit (0~5).^;
  !!el;
    !!VR(address:y):S7994448 -(MON_VAMPIRE_LORD) +(mon);
    !!UN:C(address)/(UNC_INT8)/(ability);
  !!en;
!!en;

!?FU(abil_SetUpMonsterSpecialAbility2);
!#VA(mon:x) (ability:x);
; 0 Bind - Dedroid Soildiers
; 1 20% Blind - Unicorns
; 2 205 Disease - Zombies
; 3 25% Curse - Black Knights
; 4 20% Age - Ghost Dragons
; 5 20% Stone - Basilisks
; 6 20% Paralyse - Scorpions
; 7 20% Poison - Monarch Wyvern
; 8 Acid Breath (-def) - Rust Dragons
; 9 No Ability
!!SN:L^amethyst2_4.dll^/?(amethyst:y);

!!if&(amethyst)<>0;
  !!SN:A(amethyst)/^ChangeCreatureTable^/?(address:y) E(address)/1/(mon)/^Spell effect=%(ability)^;
!!el;
  !!if&(mon)<(MON_DENDROID_GUARD);
    !!IF:M^Sorry, but you can't set special ability 1 of monsters prior than #22 by this way.^;
  !!el&(ability)<0/(ability)>9;
    !!IF:M^Sorry, but the special ability 2  you set is beyond the limit (0~9).^;
  !!el;
    !!VR(address:y):S7994592 -(MON_DENDROID_GUARD) +(mon);
    !!UN:C(address)/(UNC_INT8)/(ability);
  !!en;
!!en;

; ========================== TESTING ==========================

!?FU(OnStartOrLoad);
!!FU(abil_SetUpMonsterSpecialAbility1):P(MON_GHOST_DRAGON)/0;
!!FU(abil_SetUpMonsterSpecialAbility2):P(MON_GHOST_DRAGON)/0;
!!FU(abil_SetUpMonsterSpecialAbility1)&i^Typhon_Third_Upgrade_Mod_Active^:P(MON_GHOST_DRAGON)/0;
!!FU(abil_SetUpMonsterSpecialAbility2)&i^Typhon_Third_Upgrade_Mod_Active^:P(MON_GHOST_DRAGON)/0;
