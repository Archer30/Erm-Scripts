ZVSE2
; Author:   wessonsm & Archer30
; Engine:   ERM 2.0+
; Requires: ERA 3.9.3+, Era Erm Framework

; Funciton for getting an ideal postion near the proprosed postition for BU:S


!?FU(summ_GetValidPositionForSummon);
!#VA(pos:x);                            [proposed position]
!#VA(mon:x);                            [monster for summoning]
!#VA(side:x);                           [side of summoning]
!#VA(result:x);                         [nearest valid position for summoning. -1 for no position found]
!#VA(maxDistance:x);                    [optional parameter, max distance between the proposed position and the nearest valid position]

!!FU:A?(params:y);
!!VR(maxDistance)&(params)<5:S19; by default

!!MA:X(mon)/?(flags:y);
!!VR(isDoubleWide:y):S(flags) &(MON_FLAG_WIDE);

!!VR(result):S-1;

!!FU(summ_IfPosIsValid):P(pos)/(isDoubleWide)/(side)/?(isAvailPos:y);

!!if&(isAvailPos);
  !!VR(result):S(pos);
!!el;
  !!VR(posX:y):S(pos) %17;
  !!VR(posY:y):S(pos) :17;

  !!re (distance:y)/1/(maxDistance);
    !!FU(NewIntArray):P?(validPosArray:y);
    !!VR(steps:y):S(distance) *6 -1;

    !!re i/0/(steps);
      !!if&i=0;
        !!VR(direction:y):S3;             [Upper Left]
      !!el;
        !!VR(direction):Si :(distance);
      !!en;

      !!FU(summ_NextHex):P(direction)/?(pos)/?(posX)/?(posY);(posX) and (posY) are needed for next position calculation, if the current one is out of battlefield
      !!FU(summ_IfPosIsValid):P(pos)/(isDoubleWide)/(side)/?(isAvailPos:y);

      !!if&(isAvailPos);
        !!FU(Array_Push):P(validPosArray)/(pos);
      !!en;
    !!en;

    !!SN:M(validPosArray)/?(arraySize:y);

    !!if&(arraySize)>0;
      !!VR(lastPos:y):S(arraySize)-1;
      !!VR(resultPtr:y):R0/0/(lastPos);
      !!SN:M(validPosArray)/(resultPtr)/?(result);

      !!br;
    !!en;
  !!en;
!!en;

!?FU(summ_NextHex);
!#VA(direction:x) (hex:x) (x:x) (y:x);

!!VR(rowParity:y):S(y) &1;

!!if&(direction)=0;                     [Upper Right]
  !!VR(x):+1-(rowParity);
  !!VR(y):-1;
!!el&(direction)=1;                     [Right]
  !!VR(x):+1;
!!el&(direction)=2;                     [Lower Right]
  !!VR(x):+1-(rowParity);
  !!VR(y):+1;
!!el&(direction)=3;                     [Upper Left]
  !!VR(x):-(rowParity);
  !!VR(y):+1;
!!el&(direction)=4;                     [Left]
  !!VR(x):-1;
!!el&(direction)=5;                     [Lower Left]
  !!VR(x):-(rowParity);
  !!VR(y):-1;
!!en;

!!if&(x)>-1/(x)<17/(y)>-1/(y)<11;
  !!VR(hex):S(y) *17 +(x);
!!el;
  !!VR(hex):S-1;
!!en;

!?FU(summ_IfPosIsValid);
!#VA(pos:x) (isDoubleWide:x) (side:x) (isAvailPos:x);

!!VR(isAvailPos):S(TRUE);
!!FU(summ_IfHexIsFree):P(pos)/?(isAvailPos);

!!if&(isDoubleWide)/(isAvailPos);
  !!VR(secPos:y)&(side)=(BATTLE_LEFT):S(pos) +1;
  !!VR(secPos:y)&(side)=(BATTLE_RIGHT):S(pos) -1;
  !!FU(summ_IfHexIsFree):P(secPos)/?(isAvailPos);
!!en;

!?FU(summ_IfHexIsFree);
!#VA(pos:x) (isFree:x);

!!VR(isFree):S(TRUE);
!!VR(posX:y):S(pos) %17;

!!if|(pos)<0/(pos)>186/(posX)=0/(posX)=16;
  !!VR(isFree):S(FALSE);
!!en;

!!if&(isFree);
  !!BU:E(pos)/?(stack:y) O(pos)/?(obstacleFlag:y);
  !!VR(isFree)|(stack)>(NO_STACK)/(obstacleFlag)>0:S(FALSE);
!!en;

!!if&(isFree);
  !!UN:C(COMBAT_MANAGER)/4/?(battleMgr:y);
  !!SN:E4626848/2/(battleMgr)/(pos)/0;  defensive moat
  !!VR(isFree)&v1=1:S(FALSE);
  !!SN:E4625904/2/(battleMgr)/(pos);  if position prohibited
  ; needed for positions 95 and 96 near the town gate
  !!VR(isFree)&v1=1:S(FALSE);
!!en;

; ========================== TESTING ==========================

; Check the ideal summoning location for Archangel for clicked player by Shifh+RMB on any postion on the battlefield
!?FU(OnBattleScreenMouseClick)&i^mouse_action^=(MOUSE_RMB_PRESSED)/i^key_shift^;
!!OW:C?(player:y)/?(interactPlayer:y);

!!if&i^battle_owner_0^=(interactPlayer);
  !!VR(side:y):S(BATTLE_LEFT);
!!el;
  !!VR(side):S(BATTLE_RIGHT);
!!en;

!!CM:D?(pos:y);

!!FU(summ_GetValidPositionForSummon):P(pos)/(MON_ARCHANGEL)/(side)/?(summonPos:y)/8;
!!IF:L^proposed postion: %(pos), side: %(side), summon position: %(summonPos)^;
