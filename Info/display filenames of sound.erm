ZVSE2
; Author:   Archer30
; Engine:   ERM 2.0+
; Requires: ERA 3.3+, Era Erm Framework

; Activate/Deactivate displaying filename of sound by presssing Alt + N


!?FU(OnKeyPressed)&i^key_alt^/i^key^=(KEY_N);
!#VA(key:x) (preventDefault:x);

!!VR(preventDefault):S(TRUE);

!!if&i^soundname_on^;
  !!VRi^soundname_on^:S(FALSE);
  !!IF:L^Display filename of sound {On}^;
!!el;
  !!VRi^soundname_on^:S(TRUE);
  !!IF:L^Display filename of sound {Off}^;
!!en;

!?FU(OnSoundPlay)&i^soundname_on^;
!!SN:S?z1;
!!FU(soundname_QuietMessage):P;

!?FU(soundname_QuietMessage);
; Arguments: Message: z1;
!!VRy1:Sv2;

!!FU(soundname_SoundControl):P0/2;
!!IF:Lz1;
!!FU(soundname_SoundControl):P1/2;

!!VRv2:Sy1;

!?FU(soundname_SoundControl);
; Arguments: SoundIsOn: boolean; OldValue: v-index
!!if&x1=1;
  !!VRy4:Svx2;
  !!VRy1:Sy4%256;
  !!VRy4::256;
  !!VRy2:Sy4%256;
  !!VRy4::256;
  !!VRy3:Sy4%256;
  !!UN:C6916016/1/y1 C6916020/1/y2 C6916028/1/y3;
!!el;
  !!UN:C6916016/1/?y1 C6916020/1/?y2 C6916028/1/?y3 C6916016/1/0 C6916020/1/0 C6916028/1/0;
  !!VRy3:*65536;
  !!VRy2:*256;
  !!VRvx2:Sy1+y2+y3;
!!en;
