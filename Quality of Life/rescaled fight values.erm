ZVSE2
; Author:   Archer30
; Engine:   ERM 2.0+
; Requires: ERA 3.0+, Era Erm Framework

; Divide all fight values by 15 in order to prevent negative values after gathering massive troops.
; Suggested by Berserker after writing the recaled AI values scripts. Usually when AI values overflowed, fight values are not too far away.
; Warning: Not compatible with script that relies on fight values (not too few of them) to work correctly!


!?FU(OnBattlefieldVisible);
!!FU(fvalue_RescaleFightValues):P;

!?FU(fvalue_RescaleFightValues);
; Check if the fight value of peasant is original - exit if not
!!MA:F(MON_PEASANT)/?(fightValuePeasant:y);
!!FU&(fightValuePeasant)<>15:E;

; Get the id of the last monster
!!VR(lastMon:y):S(MON_LAST_WOG);
!!SN:L^amethyst2_4.dll^/?(amet2:y);     [compatiblity with TUM]

!!if&(amet2)<>0;
  !!UN:C5509220/4/?(lastMon);
  !!VR(lastMon):-1;
!!en;

; Divide by 15 for all fight values
!!re i/(MON_FIRST)/(lastMon);
  !!MA:Fi/d:15;
!!en;

!?FU(OnAfterBattleAction);
; Execute only when battle is about to end
!!BU:C?(battleIsEnded:y);
!!FU&(battleIsEnded)<>(TRUE):E;

!!FU(fvalue_RestoreFightValues):P;

!?FU(fvalue_RestoreFightValues);
; Check if the fight value of peasant is changed - exit if not
!!MA:F(MON_PEASANT)/?(fightValuePeasant:y);
!!FU&(fightValuePeasant)<>1:E;

; Get the id of the last monster
!!VR(lastMon:y):S(MON_LAST_WOG);
!!SN:L^amethyst2_4.dll^/?(amet2:y);     [compatiblity with TUM]

!!if&(amet2)<>0;
  !!UN:C5509220/4/?(lastMon);
  !!VR(lastMon):-1;
!!en;

; Restore all fight values
!!re i/(MON_FIRST)/(lastMon);
  !!MA:Fi/d*15;
!!en;
